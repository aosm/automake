# Process this file with autoconf to produce a configure script.

# Copyright 1995, 1996, 1997, 1998, 1999, 2000, 2001, 2002
# Free Software Foundation, Inc.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2, or (at your option)
# any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
# 02111-1307, USA.

AC_PREREQ(2.52)
AC_INIT([GNU Automake], [1.6.1], [bug-automake@gnu.org])

AC_CONFIG_SRCDIR(automake.in)
AC_CONFIG_AUX_DIR(lib)

AM_INIT_AUTOMAKE([1.6 dist-bzip2])

# The API version is the base version.  We must guarantee
# compatibility for all releases with the same API version.
# Our current rule is that:
# * All releases, including the prereleases, in an X.Y series
#   are compatible.  So 1.5.1c is compatible with 1.5.
# * Prereleases on the trunk are all incompatible -- 1.5b and 1.5c
#   aren't the same.
APIVERSION=`echo "$VERSION" | sed -e 's/^\([[0-9]]*\.[[0-9]]*[[a-z]]*\).*$/\1/'`
AC_SUBST(APIVERSION)

# A versioned directory, defined here for convenience.
pkgvdatadir="\${datadir}/automake-${APIVERSION}"
AC_SUBST(pkgvdatadir)

# $AUTOMAKE and $ACLOCAL are always run after a `cd $top_srcdir',
# hence `.' is really what we want for perllibdir and libdir.
ACLOCAL="perllibdir=./lib `pwd`/aclocal --acdir=m4 -I `pwd`/m4"
AUTOMAKE="perllibdir=./lib `pwd`/automake --libdir=lib"

AC_PATH_PROG(PERL, perl)
if test -z "$PERL"; then
   AC_MSG_ERROR([perl not found])
fi
$PERL -e 'require 5.005;' || {
   AC_MSG_ERROR([perl 5.005 or better is required])
}

# Test for Autoconf.  We run Autoconf in a subdirectory to ease
# deletion of any files created (such as those added to
# autom4te.cache).
mkdir conftest
echo 'AC''_PREREQ(2.52)' > conftest/conftest.ac
AM_RUN_LOG([cd conftest && eval $AUTOCONF -o /dev/null conftest.ac]) || {
   AC_MSG_ERROR([Autoconf 2.52 or better is required])
}
rm -rf conftest

AC_CONFIG_FILES([automake], [chmod +x automake])
AC_CONFIG_FILES([aclocal], [chmod +x aclocal])

AC_CONFIG_FILES([
  Makefile
  lib/Automake/Makefile
  lib/Makefile
  lib/am/Makefile
  m4/Makefile
  m4/amversion.m4:m4/amversion.in
  tests/Makefile
])

AC_OUTPUT
